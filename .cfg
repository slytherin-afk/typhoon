program → declaration* EOF ; 
declaration → varDecl | statement ;
varDecl → "var" varList ";" ;
varList → IDENTIFIER ( "=" assignment )? ( "," IDENTIFIER ( "=" assignment )? )* ;
statement → exprStmt | ifStmt | printStmt | block ;
exprStmt → expression ";" ; 
ifStmt → "if" "(" expression ")" statement ( "else" statement )? ;
printStmt → "print" expression ";" ;
exitStmt → “exit” expression ";" ;

expression → comma ;
comma → assignment ( "," assignment )* ;
assignment → IDENTIFIER "=" assignment | ternary ;
ternary → or ( “?” expression “:” expression )? ;  
or → and ( “or” and)* ;
and → equality ( “and” equality)* ;
equality → comparison ( ( "!=" | "==" ) comparison )* ; 
comparison → term ( ( ">" | ">=" | "<" | "<=" ) term )* ; 
term → factor ( ( "-" | "+" ) factor )* ; 
factor → unary ( ( "/" | "*" ) unary )* ;
unary → ( "!" | "-" ) unary | primary ; 
primary → IDENTIFIER | NUMBER | STRING | "true" | "false" | "nil" | "(" expression ")" ;
